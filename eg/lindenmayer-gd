#!/usr/bin/env perl
use strict;
use warnings;

# https://en.wikipedia.org/wiki/Lindenmayer_System

use Data::Turtle;
use GD;
use Getopt::Long;

my %opts = (
    rule     => 3,
    repeat   => 6,
    start    => 'X',
    distance => 3,
    theta    => 25,
    center   => 0,
    width    => 500,
    height   => 500,
);
GetOptions( \%opts,
    'rule=i',
    'repeat=i',
    'start=s',
    'distance=i',
    'theta=i',
    'center',
    'width=i',
    'height=i',
) or die "Can't get options";

my %rule = (
    # Branches: start=X, distance=20, theta=20
    1 => {
        X => 'YF[-X][+X]',
        Y => 'F',
    },

    # Koch curve: start=F, distance=10, theta=90
    2 => {
        F => 'F+F-F-F+F',
    },

    # Fractal plant: start=X, distance=3, theta=25
    3 => {
        X => 'F[-X][X]F[-X]+FX',
        F => 'FF',
    },

    # Dragon curve: start=FX, distance=10, theta=90
    4 => {
        X => 'X+YF+',
        Y => '-FX-Y',
    },

    # Sierpiński arrowhead curve: start=F, distance=1, theta=60
    5 => {
        F => 'G-F-G',
        G => 'F+G+F',
    },

    # Sierpiński triangle: start=F-G-G, distance=4, theta=120
    6 => {
        F => 'F-G+F+G-F',
        G => 'GG',
    },

    # Koch snowflake: start=F++F++F, , distance=3, theta=60
    7 => {
        F => 'F-F++F-F',
        X => 'FF',
    },

    # Sierpiński carpet: start=F, distance=4, theta=90
    8 => {
        F => 'F+F-F-F-G+F+F+F-F',
        G => 'GGG',
    },

    # Koch island: start=F-F-F-F, distance=5, theta=90
    9 => {
        F => 'F-F+F+FF-F-F+F',
    },

    # Koch islands and lakes: start=F+F+F+F, distance=5, theta=90
    10 => {
        F => 'F+f-FF+F+FF+Ff+FF-f+FF-F-FF-Ff-FFF',
        f => 'ffffff',
    },

    # "Grid": start=F-F-F-F, distance=5, theta=90
    11 => {
        F => 'FF-F-F-F-FF',
    },

    # Terndrils: start=F-F-F-F, distance=5, theta=90
    12 => {
        F => 'FF-F--F-F',
    },
);

my $turtle = Data::Turtle->new(
    width  => $opts{width},
    height => $opts{height},
    x      => $opts{width} / 2,
    y      => $opts{center} ? $opts{height} / 2 : $opts{height},
);

my $img = GD::Image->new( $opts{width}, $opts{height} );

my %color = (
    white => $img->colorAllocate( 255, 255, 255 ),
    black => $img->colorAllocate( 0, 0, 0 ),
);

$img->transparent( $color{white} );
$img->interlaced('true');

my @statestack;

my %translate = (
    'f' => sub { $turtle->forward( $opts{distance} ) },
    'F' => sub {
        my @line = $turtle->forward( $opts{distance} );
        if ( $turtle->pen_status == 1 && @line ) {
            $img->setThickness( $line[5] );
            $img->line( @line[ 0 .. 3 ], $color{ $line[4] } );
        }
    },
    'G' => sub {
        my @line = $turtle->forward( $opts{distance} );
        if ( $turtle->pen_status == 1 && @line ) {
            $img->setThickness( $line[5] );
            $img->line( @line[ 0 .. 3 ], $color{ $line[4] } );
        }
    },
    '-' => sub { $turtle->turn( - $opts{theta} ) },
    '+' => sub { $turtle->turn( $opts{theta} ) },
    'M' => sub { $turtle->mirror },
    '[' => sub { push @statestack, [ $turtle->get_state ] },
    ']' => sub { $turtle->set_state( @{ pop @statestack } ) },
);

for ( 1 .. $opts{repeat} ) {
    $opts{start} =~ s/(.)/defined($rule{ $opts{rule} }{$1}) ? $rule{ $opts{rule} }{$1} : $1/eg;
}
warn "$opts{start}\n";

for my $command ( split //, $opts{start} ) {
    $translate{$command}->() if exists $translate{$command};
}

print $img->png;
